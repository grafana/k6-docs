DEBUG := false

fmt := import("fmt")
text := import("text")

matches := []
ruleActive := true

cursor := 0
for line in text.split(scope, "\n") {
  if DEBUG {
    fmt.println(text.pad_left(text.itoa(cursor), 3, " "), ": ",  text.quote(line), " ", len(line))
  }

  if text.re_find(`<!-- vale Grafana.SmartQuotes = NO -->`, line, -1) {
    ruleActive = false
  }

  if text.re_find(`<!-- vale Grafana.SmartQuotes = YES -->`, line, -1) {
    ruleActive = true
  }

  if ruleActive {
    for match in text.re_find(`[‘’ʼ“”]`, line, -1) {
      matches = append(matches, {begin: cursor + match[0].begin, end: cursor + match[0].end})
    }
  }

  cursor += len(line) + 1
}

if DEBUG {
  for match in matches {
    fmt.println(match)
  }
}